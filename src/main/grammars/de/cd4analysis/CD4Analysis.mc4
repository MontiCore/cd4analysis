package de.cd4analysis;

/*
   MCG for CD 4 Analysis, Version August 17th, 2014
*/

grammar CD4Analysis extends de.monticore.types.Types {

 CDCompilationUnit =
 	("package" Package:(Name& || ".")* ";")? 
  	(ImportStatements:ImportStatement)*
  	CDDefinition;


 CDDefinition = "classdiagram" Name "{"
    (   cDClasses:CDClass
      | cDInterfaces:CDInterface
      | cDEnums:CDEnum
      | cDAssociations:CDAssociation)* "}";
  
  CDClass = Modifier?  "class" Name
    ( "extends"  superclasses:ReferenceType)?
    ( "implements" interfaces:(ReferenceType || ",")* )?
    ( "{" 
      ( cDAttributes:CDAttribute
      | cDConstructors:CDConstructor
      | cDMethods:CDMethod )*
      "}" 
    | ";" );
  
  CDInterface = "interface" Name
    ( "extends" interfaces:(ReferenceType || ",")* )? ( "{" 
      ( cDAttributes:CDAttribute
      | cDMethods:CDMethod )*
      "}" 
    | ";" );
    
  CDEnum = "enum" Name
    ( "implements" interfaces:(ReferenceType || ",")* )?
    ( "{" ( cDEnumConstants:(CDEnumConstant || ",")* 
    ( cDConstructors:CDConstructor | cDMethods:CDMethod)*
    ";")?
      "}" | ";" );
  
  
  CDEnumConstant = Name;
  
  CDAttribute = Modifier?  Type Name ("=" Value)? ";";
  
  CDConstructor =
    Modifier
    TypeParameters?
    Name "(" (cDParameters:(CDParameter || ",")*)? ")"
    ("throws" exceptions:(QualifiedName || "," )*)?
    ";";
        
  CDMethod =
    Modifier
    TypeParameters?
    ReturnType
    Name "(" (cDParameters:(CDParameter || ",")*)? ")"
    ("throws" exceptions:(QualifiedName || ",")*)?
    ";";
  
  CDParameter =
    Type (Ellipsis:["..."])? Name;
        
  CDAssociation = Stereotype? 
    (["association"] | ["composition"])
    (Derived:[DERIVED:"/"])?  Name?
    leftModifier:Modifier?
    leftCardinality:Cardinality?
    leftReferenceName:QualifiedName
    ("[" leftQualifier:CDQualifier "]")?
    ("(" leftRole:Name ")")?
    (   leftToRight:["->"]
      | rightToLeft:["<-"]
      | bidirectional:["<->"]
      | simple:["--"] )
    ("(" rightRole:Name ")")?
    ("[" rightQualifier:CDQualifier "]")?
    rightReferenceName:QualifiedName
    rightCardinality:Cardinality?
    rightModifier:Modifier ";" ;
  
  Modifier = Stereotype?
    (["abstract"]
      | ["final"]
      | ["static"]
      | Private:["private"] | Private:[PRIVATE:"-"]  // TODO: Vereinfachung (Bug)
      | Protected:["protected"] | Protected:[PROTECTED:"#"]
      | Public:["public"] | Public:[PUBLIC:"+"]
      | Derived:["derived"] | Derived:[DERIVED:"/"] )*;
    
  Cardinality = many:["[*]"] | one:["[1]"] | oneToMany:["[1..*]"] | optional:["[0..1]"];
   
  CDQualifier = Name | Type;
  
  Stereotype = "<<" values:(StereoValue || ",")* ">>";
  
  StereoValue = Name;
  
  Value = Literal;
}
