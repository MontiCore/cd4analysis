/* (c) https://github.com/MontiCore/monticore */
package de.monticore.cd;

/* Alpha-version: This is intended to become a MontiCore stable grammar. */

/**
   This component grammar describes the method signatures that 
   are used by CD4Code.
   
   It contains method and constructor signatures including 
   visibility modifiers.

   It uses CDBasis.mc4, which allows a rather free
   combination with other extensions.
*/

component grammar CD4CodeBasis extends
    de.monticore.cd.CDBasis,
    de.monticore.cd.CDInterfaceAndEnum,
    de.monticore.expressions.CommonExpressions {

  /* ==== Methods ==== */
  /** Introduction of the new form of symbol: the Method
   */
  interface scope symbol CDMethodSignature extends CDMember, Method = Name;

  CDThrowsDeclaration = "throws" exception:(MCQualifiedName || ",")+;

  /** The standard (Java) method
   */
  CDMethod implements CDMethodSignature =
    CDModifier*
    MCReturnType
    Name "(" (CDParameter || ",")* ")"
    CDThrowsDeclaration?
    ";";

  /** The standard (Java) constructor (without return type)
   */
  CDConstructor implements CDMethodSignature =
    CDModifier*
    Name "(" (CDParameter || ",")* ")"
    CDThrowsDeclaration?
    ";";

  /** Method parameter: with type, name, default value,
      and list extension "..."
   */
  CDParameter implements Field = // TODO extend CDAttribute?; Modifier?
    MCType (ellipsis:["..."])? Name ("=" defaultValue:Expression)?;

  /* ==== Modifier ==== */
  /** This list of modifiers extends the CD4A modifiers.
   */
  CD4CodePrivateModifier   implements CDDirectModifier = "private"        | "-";
  CD4CodeProtectedModifier implements CDDirectModifier = "protected"      | "#";
  CD4CodePublicModifier    implements CDDirectModifier = "public"         | "+";
  CD4CodeDerivedModifier   implements CDDirectModifier = key("derived")   | "/";
  CD4CodeReadOnlyModifier  implements CDDirectModifier = key("readonly");

  /** Enumeration constants may now also be defined using arguments
      (like in Java)
   */
  CD4CodeEnumConstant extends CDEnumConstant =
    Name Arguments?;

  // !! BR TODO "MethodType" intendiert, dass hier sowohl argument als auch Ergebnis
  //  typisiert sind. --> Parameter fehlen.: Parameter stehen schon im MethodSymbol
  // BR 22.5: Wenn die Parameter im MethodSymbol stehen, warum dann nicht die "hasEllipsis"
  // Ausserdem sind Ergebnistyp und Exceptions auch stärker zu verzahnen 
  // (denn von der Logik her gehören die auch zusammen)
  /* ==== Symbols ==== */
  /** Method knows its signature, whether it is a constructor, 
      visibility
   */
  symbolrule CDMethodSignature =
    hasEllipsis: boolean // TODO auch in MethodSymbol rein bauen?
    exceptions: de.monticore.types.check.SymTypeExpression*
    modifier: de.monticore.cd.cdbasis._symboltable.SymModifier
  ;
}
