/* (c) https://github.com/MontiCore/monticore */
package de.monticore;

/* This is a MontiCore stable grammar.
 * Adaptations -- if any -- are conservative. */

/**
   This component grammar defines interfaces and enum classes 
   for CD4Analysis and CD4Code.

   This includes directions, qualifiers, multiplicities and compositions.   
*/

component grammar CDInterfaceAndEnum extends de.monticore.CDBasis {

  /** An interface is quite similar to a class:
   */
  scope CDInterface implements CDType =
    Modifier "interface" Name
    CDExtendUsage?
    ( "{"
        CDMember*
      "}"
    | ";" );

  /** Enumerations allow to define a list of names that act as constants.
      Please note that Java allows attributes for enums:
      this possibility will be added in CD4Code, but not yet here.
   */
  scope CDEnum implements CDType =
    Modifier "enum" Name
    CDInterfaceUsage?
    ( "{"
        (CDEnumConstant || ",")* ";"
        CDMember*
      "}"
    | ";" );

  /** Have the enum constants as separate NT. To allow extensions
      an enum constant is a static final attribute of the enum with the type
      of the enum
  */
  CDEnumConstant implements Field = Name;
}
