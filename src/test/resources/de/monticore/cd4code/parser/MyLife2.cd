/* (c) https://github.com/MontiCore/monticore */
package de.monticore.cd4code.parser;

classdiagram MyLife2 {

  package entity {
    interface CorporateEntity {
      int vatNumber;
    }
  }

  public abstract class Person implements CorporateEntity {
    protected java.lang.String name;
    int age;
    java.util.Date birthday;
    java.util.List<java.lang.String> nickNames;
    public Person(java.lang.String name);
    java.util.Set<Person> getParents();
  }

  package uni {
    class Student extends Person {
      StudentStatus status;
      -> Address [1..*] {ordered};
      abstract public change(StudentStatus status);
      void addFriends(Person... friends);
    }

    enum StudentStatus {
      ENROLLED,
      FINISHED;
    }
  }

  class Grades;
  class PhoneNumber;
  class Address;

  composition Student -> Grades [*];
  association [0..1] Person (parent) <-> (child) Person [*];
  association phonebook Person [java.lang.String] -> PhoneNumber;
}
