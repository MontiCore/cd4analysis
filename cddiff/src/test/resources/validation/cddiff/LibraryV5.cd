package examples.cd;

import java.util.Date;
import java.lang.String;

/**
 Changes in attributes only: 
 - Item has an additional attribute String "source"
 - Book has an additional attribute int "price"
 - AvailablityState enum has another value "repair"
**/
classdiagram LibraryV5 {

    abstract class Item {
        String title;
        //Publisher publisher;
        Date published;
        Language lang;
        String source;
    }
    
    class Book extends Item {
        int numOfPages;
        String isbn;
        int edition;
        int price;
    }
    
    class Map extends Item {
        String area;
        Date date;
        //Publisher publisher;
    }
    
    class NewsPaper extends Book;
    
    class Publisher {
        String name;
        String address;
        Date estDate;
        String phoneNumber;
    }
    
    class Copy {
        int id;
        BookCondition cond;
        AvailabilityState state;
        Medium medium;      
    }
    
    enum BookCondition { new, used, broken; }
    enum Medium {HardCover, PaperBack, EBook, AudioCD;}
    enum AvailabilityState {available, taken, missing, repair; }
    enum Language {German, English; }
    
    class Library;
    
    class Person {
        String name;
        String address;
    }
    
    class Reader extends Person;
    
    class Author {
        String name;
        String address;
    }
  
    class Loan {
        Date startDate;
        Date returnDate;
        int numOfExtensions;
    }
    
    class Comment {
    	Date date;
    	String comment;
    }
    
    composition Copies [1] Book (isOf) -> (hasCopy) Copy [1..*];
    association Comments [1] Loan (commentsOn) <-> (hasComment) Comment [*];
    association Loans [1] Copy (borrowedCopy) <-> (loaned) Loan [*];
    association Loaner [1] Reader (loans) <-> (readersLoan) Loan [*];
    association AuthorsOfBook [1..*] Author (writtenBy) <-> (authorOf) Book [1..*];
    
    association ReaderOfLibrary [*] Reader (readers) <-> (knownLibraries) Library [1..*];
    association ReaderOfLibrary [*] Copy (ownedCopies) <-> (copiesHome) Library [1];
    
    association Publishing [*] Item (publish) -> (publishedBy) Publisher [1];
}