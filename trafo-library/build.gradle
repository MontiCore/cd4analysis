/* (c) https://github.com/MontiCore/monticore */
plugins {
  id "java"
  id "java-library"
  id "de.monticore.generator" version "$mc_version" // MontiCore Plugin
  id "maven-publish"
}
import de.monticore.MontiTransExec


group = "de.monticore.lang"
description = "trafo-library"
sourceCompatibility = JavaVersion.VERSION_11
buildDir = file("$projectDir/target")


def grammarDir = "src/main/grammars"
def grammarOutDir = "$buildDir/generated-sources"
def testGrammarOutDir = "$buildDir/generated-test-sources/mc"


// configure non-standard source sets
sourceSets {
  test {
    java.srcDirs += [ "$testGrammarOutDir"]
  }
  main {
    java.srcDirs += [ "$grammarOutDir"]

  }
  grammars {
    resources {
      srcDirs(grammarDir)
      include "**/*.mc4"
    }
  }
}

configurations { grammar }


dependencies {
  grammar group: 'de.monticore', name: 'monticore-grammar-trafo', version: mc_version
  grammar group: 'de.monticore', name: 'monticore-grammar', version: mc_version

  // Depend on cd4a-trafos - This is done both as a local project, and with proper dependency substitution
  // add the dependency to cd4analysis trafos to the grammar config,
  // adding it as a dependency to the trafo publication
  // Note: This dependency will be substituted by its subproject
  grammar(group: project.getGroup(), name: "cd4analysis-trafo", version: project.getVersion())
  // use the grammar configuration (for trafos) to add transitive dependencies of :cdlang
  grammar(project(path: ":cdlang", configuration: 'trafoGrammar'))
  // add the outgoing trafo grammar (symbols/models) of the subproject to the modelpath
  grammarSymbolDependencies project(path: ':cdlang', configuration: 'trafoGrammarSymbolOutElements')
  // add the compiled trafo java classes of the subproject to the classpath
  implementation files(project(":cdlang").sourceSets.trafo.output.classesDirs)


  implementation group: 'de.se_rwth.commons', name: 'se-commons-groovy', version: commons_version

  testImplementation "org.junit.jupiter:junit-jupiter-api:$junit_version"
  testImplementation "org.junit.jupiter:junit-jupiter-params:$junit_version"
  testImplementation "org.junit.vintage:junit-vintage-engine:$junit_version"
  testImplementation "org.mockito:mockito-inline:$mockito_version"
  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit_version"

  compileOnly 'org.freemarker:freemarker:2.3.28'
}

configurations.configureEach {
  resolutionStrategy.dependencySubstitution {
    // Substitute cd4lang-trafo with the actual (live) subprojects trafo variant
    substitute module("de.monticore.lang:cd4analysis-trafo") using variant(project(":cdlang"), {
      attributes {
        it.attribute(Attribute.of('monticore.generator.sourceset', String), "trafo")
      }
    })
    // Also substitute :cdlang with its trafo variant
    substitute project(":cdlang") using variant(project(":cdlang"), {
      attributes {
        it.attribute(Attribute.of('monticore.generator.sourceset', String), "trafo")
      }
    })
  }
}

repositories {
  if (("true").equals(getProperty('useLocalRepo'))) {
    mavenLocal()
  }
  mavenCentral()
  maven {
    credentials.username mavenUser
    credentials.password mavenPassword
    url repo
  }
}

// Fixes issue with java plugin in projects without resources
compileTestJava.doFirst { mkdir sourceSets.test.output.resourcesDir }

tasks.withType(Test) {
  maxParallelForks = Runtime.runtime.availableProcessors() ?: 1
}


java {
  withSourcesJar()
  registerFeature('grammars') {
    usingSourceSet(sourceSets.grammars)
  }
}

jar.dependsOn grammarsJar

task buildCD4ATrafoLibTrafos {}

fileTree(dir: "$projectDir/src/main/transformations", include: '**/**.mtr').each {
  def g = it
  def taskname = "buildCD4ATrafoLibTrafos${it.getName().substring(0, it.getName().lastIndexOf('.'))}"

  task "$taskname" (type: MontiTransExec.class){
    getClassPath().setFrom(
      project(":cdlang").configurations.trafoRuntimeClasspath,
      project(":cdlang").sourceSets.trafo.output
    )
    TFGenTool = 'de.monticore.tr.CD4CodeTFGenTool'
    input = file(g)
    outputDir = file("$buildDir/generated-sources")
    tasks.sourcesJar.dependsOn it
  }
  buildCD4ATrafoLibTrafos.dependsOn("$taskname")
  generateMCGrammars.dependsOn += "$taskname"
}

compileJava.dependsOn += buildCD4ATrafoLibTrafos

task buildAll(type: GradleBuild) {
  tasks = ['build']
}

jar {
  archiveClassifier = null
}

// configure deployment
publishing {
  // configure what artifacts to publish
  publications {
    mavenJava(MavenPublication) {
      suppressPomMetadataWarningsFor('grammarsApiElements')
      suppressPomMetadataWarningsFor('grammarsRuntimeElements')
      from components.java
    }
  }
  repositories.maven {
    credentials.username mavenUser
    credentials.password mavenPassword
    def releasesRepoUrl = "https://nexus.se.rwth-aachen.de/content/repositories/monticore-releases/"
    def snapshotsRepoUrl = "https://nexus.se.rwth-aachen.de/content/repositories/monticore-snapshots/"
    url = version.endsWith("SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl
  }
}
