/* (c) https://github.com/MontiCore/monticore */
plugins {
  id "com.github.johnrengelman.shadow" version "6.0.0"
}

description = "CD4Analysis: Tool"

configurations {
  // An extra configuration that is used by the shadow plugin, since we don't want cd2smt or plantuml in the tools jar.
  // All its dependencies will be automatically added to implementation
  implementationWithoutExtras
}
configurations.implementation.extendsFrom(configurations.implementationWithoutExtras)

dependencies {
  implementationWithoutExtras(project(':cdlang'))
  implementationWithoutExtras(project(":cddiff"))
  implementationWithoutExtras(project(":cdmerge"))
  // Our classes can be used to pretty print to plantuml, but we dont want all dependencies for plantuml
  // in the tool
  implementationWithoutExtras(project(":cd2plantuml")){
    transitive = false
  }

  implementation(project(":cd2plantuml"))
  implementation(project(":cd2smt"))

  implementationWithoutExtras "de.monticore:monticore-grammar:${project.properties['mc_version']}"
  implementationWithoutExtras "de.monticore:monticore-runtime:${project.properties['mc_version']}"
  implementationWithoutExtras "de.monticore:class2mc:${project.properties['mc_version']}"
  implementationWithoutExtras "de.se_rwth.commons:se-commons-logging:$commons_version"
  implementationWithoutExtras "de.se_rwth.commons:se-commons-utilities:$commons_version"
  implementationWithoutExtras "com.google.guava:guava:$guava_version"
  implementationWithoutExtras "com.fasterxml.jackson.core:jackson-databind:$jackson_version"
  implementationWithoutExtras "commons-cli:commons-cli:$commons_cli_version"
  implementationWithoutExtras "org.apache.commons:commons-lang3:$commons_lang_version"
  implementationWithoutExtras "org.antlr:antlr4-runtime:$antlr_version"
  testImplementation "org.freemarker:freemarker:$freemarker_version"
  testImplementation "org.junit.jupiter:junit-jupiter-api:$junit_version"
  testImplementation "org.junit.jupiter:junit-jupiter-params:$junit_version"
  testImplementation "org.junit.vintage:junit-vintage-engine:$junit_version"
  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit_version"
  testImplementation ("de.monticore:monticore-runtime:${project.properties['mc_version']}") capabilities {
    requireCapability("de.monticore:monticore-runtime-tests") }
  testImplementation "org.mockito:mockito-core:$mockito_version"
  testImplementation group: 'com.google.code.findbugs', name: 'jsr305', version: findbugs_version
  testImplementation "de.monticore:monticore-grammar-trafo:$mc_version"
  testImplementation(project(':cdlang').sourceSets.test.output)
  testImplementation project(path: ':cdlang')
  implementationWithoutExtras("de.monticore.lang:od:$mc_version") {
    exclude group: 'de.monticore.lang', module:'cd4analysis'
  }
}

task toolJar(type: Jar) {
  from sourceSets.main.output + sourceSets.main.allSource
  archiveClassifier = "tool"
}


apply plugin: "com.github.johnrengelman.shadow"
  shadowJar {
    configurations = [project.configurations.implementationWithoutExtras]
    manifest {
      attributes "Main-Class": "de.monticore.CD4CodeTool"
    }
    archiveFileName = "MCCD.jar"
    archiveClassifier = "mc-tool"
  }
assemble.dependsOn(shadowJar)


jar.dependsOn(shadowJar)

// Test the CLI-jar (produced by the shadowJar task)
sourceSets {
  tooltest
}

dependencies {
  tooltestImplementation platform('org.junit:junit-bom:5.9.1')
  tooltestImplementation 'org.junit.jupiter:junit-jupiter'
}

tasks.register('toolTest', Test) {
  description = 'Runs tool integration tests.'
  group = 'verification'

  testClassesDirs = sourceSets.tooltest.output.classesDirs
  classpath = sourceSets.tooltest.runtimeClasspath
  shouldRunAfter test
  dependsOn shadowJar

  useJUnitPlatform()

  testLogging {
    events "passed"
  }

  // Pass path to the tool jar to the test
  systemProperty "toolJarFile",  tasks.shadowJar.outputs.files.singleFile
  // Pass mc_version to the test
  systemProperty "mc_version",  mc_version
}

check.dependsOn toolTest
